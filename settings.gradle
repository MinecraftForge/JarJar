pluginManagement {
    repositories {
        gradlePluginPortal()
        mavenCentral()
        mavenLocal()
    }
}

plugins {
    id 'org.gradle.toolchains.foojay-resolver-convention' version '1.0.0'
    id 'net.minecraftforge.gitversion' version '3.1.0'
}

rootProject.name = 'JarJar'

includeBuild 'jarjar-gradle'
includeBuild 'jarjar-gradle-demo'
include 'filesystems', 'metadata', 'selector'

project(':filesystems').name = 'JarJarFileSystems'
project(':metadata').name = 'JarJarMetadata'
project(':selector').name = 'JarJarSelector'

enableFeaturePreview 'TYPESAFE_PROJECT_ACCESSORS'

dependencyResolutionManagement {
    repositories {
        mavenCentral()
    }

    //@formatter:off
    versionCatalogs.register('libs') {
        plugin 'javadoc-links', 'io.freefair.javadoc-links'      version '8.14'
        plugin 'gradleutils',   'net.minecraftforge.gradleutils' version '3.3.0'
        plugin 'changelog',     'net.minecraftforge.changelog'   version '3.1.1'
        plugin 'licenser',      'net.minecraftforge.licenser'    version '1.2.0'
        plugin 'shadow',        'com.gradleup.shadow'            version '9.0.2'

        library 'maven-artifact', 'org.apache.maven', 'maven-artifact' version '3.8.1'

        library 'gson', 'com.google.code.gson', 'gson' version '2.8.5'

        library 'guava', 'com.google.guava', 'guava' version '30.1.1-jre'

        library 'noexception', 'com.machinezoo.noexception', 'noexception' version '1.7.1'

        version 'slf4j', '1.7.30'
        library 'slf4j-api',    'org.slf4j', 'slf4j-api'    versionRef 'slf4j'
        library 'slf4j-simple', 'org.slf4j', 'slf4j-simple' versionRef 'slf4j'

        version 'junit', '5.8.2'
    }
    //@formatter:on
}
