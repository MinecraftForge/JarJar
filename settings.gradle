pluginManagement {
    repositories {
        gradlePluginPortal()
        maven {
            name = 'MinecraftForge'
            url = 'https://maven.minecraftforge.net/'
        }
    }

    plugins {
        id 'net.minecraftforge.gradleutils' version '2.+'
    }
}

plugins {
    id 'org.gradle.toolchains.foojay-resolver-convention' version '0.7.0'
}

rootProject.name = 'JarJar'

enableFeaturePreview 'TYPESAFE_PROJECT_ACCESSORS'

dependencyResolutionManagement {
    versionCatalogs {
        libs {
            library('apacheMaven', 'org.apache.maven', 'maven-artifact').version('3.8.1')

            library('gson', 'com.google.code.gson', 'gson').version('2.8.5')

            library('guava', 'com.google.guava', 'guava').version('30.1.1-jre')

            library('noExceptions', 'com.machinezoo.noexception', 'noexception').version('1.7.1')

            version('slf4j', '1.7.30')
            library('slf4j-api', 'org.slf4j', 'slf4j-api').versionRef('slf4j')
            library('slf4j-simple', 'org.slf4j', 'slf4j-simple').versionRef('slf4j')

            version('junit', '5.8.2')
            library('junit-api', 'org.junit.jupiter', 'junit-jupiter-api').versionRef('junit')
            library('junit-engine', 'org.junit.jupiter', 'junit-jupiter-engine').versionRef('junit')
        }
    }
}

include 'filesystems', 'metadata', 'selector'

project(':filesystems').name = 'JarJarFileSystems'
project(':metadata').name = 'JarJarMetadata'
project(':selector').name = 'JarJarSelector'
