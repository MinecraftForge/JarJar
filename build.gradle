import net.minecraftforge.gradleutils.PomUtils

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'net.minecraftforge.licenser' version '1.0.1'
    id 'net.minecraftforge.gradleutils' version '[2.3,2.4)'
}

group = 'net.minecraftforge'
version = gradleutils.tagOffsetVersion
println "Version: $version"

subprojects { Project subproject ->

    subproject.apply {
        plugin 'java-library'
        plugin 'maven-publish'
        plugin 'net.minecraftforge.licenser'
    }

    subproject.group = 'net.minecraftforge'
    subproject.version = rootProject.version

    sourceSets {
        test {
            java {
                srcDir file('src/binks/java')
            }
            resources.srcDir file('src/binks/resources')
        }
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
        withSourcesJar()
    }

    repositories {
        mavenCentral()
        maven gradleutils.forgeMaven
    }

    dependencies {
        testImplementation libs.junit.api
        testRuntimeOnly libs.junit.engine
    }

    project.tasks.withType(JavaCompile).configureEach {
        JavaToolchainService service = project.extensions.getByType(JavaToolchainService)
        Provider<JavaCompiler> compiler = service.compilerFor(s -> s.languageVersion.set(JavaLanguageVersion.of(8)))

        javaCompiler = compiler
    }

    tasks.named('test', Test).configure {
        useJUnitPlatform()
    }

    tasks.named('processTestResources', ProcessResources).configure {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }

    tasks.named('jar', Jar).configure {
        manifest {
            attributes([
                    'Maven-Artifact'          : "${project.group}:${project.archivesBaseName}:${project.version}",
                    "Specification-Title"     : project.name,
                    "Specification-Vendor"    : "Minecraft Forge",
                    "Specification-Version"   : "1", // We are version 1 of ourselves
                    "Implementation-Title"    : project.name,
                    "Implementation-Version"  : "${project.version}",
                    "Implementation-Vendor"   : "Minecraft Forge",
                    "Automatic-Module-Name"   : project.name,
            ])
        }
    }

    license {
        header = file('../header.txt')
        newLine = false
        exclude '**/*.properties'
    }

    publishing {
        publications.register('mavenJava', MavenPublication) {
            from components.java

            pom {
                name = project.name
                description = 'Jar in Jar Utilities including the required NIO FileSystems. SubSystem: ' + project.name
                url = 'https://github.com/MinecraftForge/JarJar'

                PomUtils.setGitHubDetails(pom, 'JarJar')

                license PomUtils.Licenses.LGPLv2_1
            }
        }

        repositories {
            maven gradleutils.publishingForgeMaven
        }
    }

}

changelog {
    from '0.3'
}
