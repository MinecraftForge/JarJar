import net.minecraftforge.gradleutils.GradleUtilsExtensionForProject
import net.minecraftforge.licenser.LicenseExtension
import org.gradle.api.plugins.jvm.JvmTestSuite
import org.gradle.api.tasks.testing.logging.TestExceptionFormat

plugins {
    id 'io.freefair.javadoc-links' version '8.14' apply false
    id 'net.minecraftforge.gradleutils' version '3.3.0' apply false
    id 'net.minecraftforge.changelog' version '3.1.1'
    id 'net.minecraftforge.licenser' version '1.2.0' apply false
}

group = 'net.minecraftforge'
//version set by git version in settings.gradle

changelog {
    from '0.3'
    publishAll = true
    includeSubprojects = true
}

subprojects {
    apply {
        plugin 'java-library'
        plugin 'jvm-test-suite'
        plugin 'idea'
        plugin 'maven-publish'
        plugin 'io.freefair.javadoc-links'
        plugin 'net.minecraftforge.gradleutils'
        plugin 'net.minecraftforge.changelog'
        plugin 'net.minecraftforge.licenser'
    }

    //noinspection GroovyAccessibility -- IntelliJ is stupid
    final gradleutils = (GradleUtilsExtensionForProject) gradleutils

    description = 'Jar in Jar Utilities including the required NIO FileSystems. SubSystem: ' + project.name
    group = 'net.minecraftforge'
    //version set by git version in settings.gradle

    ((JavaPluginExtension) java).tap {
        toolchain.languageVersion = JavaLanguageVersion.of(8)
        withSourcesJar()
    }

    ((TestingExtension) testing).tap {
        suites.named('test', JvmTestSuite) {
            useJUnitJupiter(libs.versions.junit)

            targets.configureEach {
                testTask.configure {
                    ignoreFailures = true
                    javaLauncher = javaToolchains.launcherFor { languageVersion = JavaLanguageVersion.of(17) }
                    testLogging { exceptionFormat = TestExceptionFormat.FULL }
                }
            }
        }
    }

    tasks.named('processTestResources', ProcessResources) {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }

    tasks.named('jar', Jar) {
        manifest {
            attributes([
                'Maven-Artifact'        : "${project.group}:${base.archivesName}:${project.version}",
                'Specification-Title'   : project.name,
                'Specification-Vendor'  : 'Forge Development LLC',
                'Specification-Version' : "1", // We are version 1 of ourselves
                'Implementation-Title'  : project.name,
                'Implementation-Version': project.version,
                'Implementation-Vendor' : 'Forge Development LLC',
                'Automatic-Module-Name' : project.name,
            ])
        }
    }

    ((LicenseExtension) license).tap {
        header = rootProject.file('LICENSE-header.txt')
        newLine = false
        exclude '**/*.properties'
    }

    ((PublishingExtension) publishing).tap {
        repositories {
            maven gradleutils.publishingForgeMaven
        }

        publications.register('mavenJava', MavenPublication) {
            from components.java

            gradleutils.promote(it)

            pom {
                name = project.name
                description = project.description

                gradleutils.pom.addRemoteDetails(pom)

                licenses {
                    license gradleutils.pom.licenses.LGPLv2_1
                }
            }
        }
    }

}
